plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.10'
    id 'fabric-loom' version '1.3.9'
}

group = project.mod_group
archivesBaseName = project.mod_name

ext.versionRevision = 'git rev-list --count HEAD'.execute().text.trim()
ext.versionHash = 'git rev-parse --short HEAD'.execute().text.trim()
version = "0.0.${ext.versionRevision}-${ext.versionHash}"

loom {
    accessWidenerPath = file 'src/main/resources/mineseekdestroy.accesswidener'
}

repositories {
    mavenCentral()
    maven {
        name = 'ViaVersion'
        url = uri 'https://repo.viaversion.com/'
    }
}

configurations {
    includeImplementation

    implementation {
        extendsFrom includeImplementation
    }
    include {
        extendsFrom includeImplementation
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}:v2"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    includeImplementation "org.jetbrains.kotlin:kotlin-stdlib:${project.kotlin_version}"
    includeImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${project.kotlin_version}"
    includeImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${project.kotlin_version}"

    includeImplementation "com.viaversion:viaversion-api:${project.viaversion_version}"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
    options.release = 17
    options.encoding = 'UTF-8'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '17'
        apiVersion = '1.9'
        languageVersion = '1.9'
        freeCompilerArgs += [
            '-Xno-call-assertions',
            '-Xno-receiver-assertions',
            '-Xno-param-assertions',
        ]
    }
}

processResources {
    filesMatching('fabric.mod.json') {
        expand 'version': version
    }
}
